plugins {
    id "java"
    id "org.jetbrains.intellij" version "0.6.5"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
}

group "org.jetbrains"
version "1.3"

repositories {
    mavenCentral()

    // This is for tinypinyin from AceJump
    jcenter()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "junit:junit:4.13"
}

intellij {
    version = "2020.3"

    if (Boolean.valueOf(aceJumpFromMarketplace)) {
        plugins += "AceJump:3.6.3"
    } else {
        plugins += project(":AceJump")
    }
    if (Boolean.valueOf(ideaVimFromMarketplace)) {
        plugins += "IdeaVIM:0.64"
    } else {
        plugins += project(":IdeaVIM")
    }

    updateSinceUntilBuild false
    buildSearchableOptions.enabled false

    publishPlugin {
        username publishUsername
        token publishToken
    }
}

compileKotlin.kotlinOptions {
    jvmTarget = "1.8"
}
compileTestKotlin.kotlinOptions {
    jvmTarget = "1.8"
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
