plugins {
    id "java"
    id "org.jetbrains.intellij" version "1.6.0"
    id "org.jetbrains.kotlin.jvm" version "1.5.0"
}

group "org.jetbrains"
version "1.8"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "junit:junit:4.13.2"
}

intellij {
    version = "2021.3.1"

    if (Boolean.valueOf(aceJumpFromMarketplace)) {
        plugins.add("AceJump:3.8.7")
    } else {
        plugins.add(project(":AceJump"))
    }
    if (Boolean.valueOf(ideaVimFromMarketplace)) {
        plugins.add("IdeaVIM:1.10.2")
    } else {
        plugins.add(project(":IdeaVIM"))
    }

    updateSinceUntilBuild = false
    buildSearchableOptions.enabled = false

    publishPlugin {
        token = publishToken
    }
}

compileKotlin.kotlinOptions {
    jvmTarget = "11"
}
compileTestKotlin.kotlinOptions {
    jvmTarget = "11"
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

tasks {
    wrapper {
        gradleVersion = gradleVersion
    }

    test {
        setScanForTestClasses(false)
        // Only run tests from classes that end with "Test"
        include("**/*Test.class")
        include("**/*test.class")
        include("**/*Tests.class")
        exclude("**/ParserTest.class")
    }
}
